# Enable Powerlevel10k instant prompt. Should stay close to the top of ~/.zshrc.
# Initialization code that may require console input (password prompts, [y/n]
# confirmations, etc.) must go above this block; everything else may go below.
if [[ -r "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh" ]]; then
  source "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh"
fi

# Download prezto if not present
[[ -d ${ZDOTDIR:-~}/.zprezto ]] ||
  git clone --recursive https://github.com/sorin-ionescu/prezto.git "${ZDOTDIR:-$HOME}/.zprezto"

# Download zsh-defer if not present
[[ -d ${HOME}/.zsh-defer ]] ||
  git clone https://github.com/romkatv/zsh-defer.git ${HOME}/.zsh-defer


# Source Prezto.
if [[ -s "${ZDOTDIR:-$HOME}/.zprezto/init.zsh" ]]; then
  source "${ZDOTDIR:-$HOME}/.zprezto/init.zsh"
fi


# Load my customizations
function load_custom() {
  export ZSH=$HOME/.dotfiles
  export DOTFILES=$HOME/.dotfiles

  typeset -U config_files
  config_files=($DOTFILES/**/*.zsh)

  # load the path files
  for file in ${(M)config_files:#*/path.zsh}
  do
    source $file
  done

  # load everything but the path and completion files
  for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
  do
    source $file
  done

  if [[ -a ~/.localrc ]]
  then
    source ~/.localrc
  fi

  unset config_files
}

source ~/.zsh-defer/zsh-defer.plugin.zsh
# defer the loading of my customizations until the prompt is drawn
zsh-defer load_custom

# To customize prompt, run `p10k configure` or edit ~/.p10k.zsh.
[[ ! -f ~/.p10k.zsh ]] || source ~/.p10k.zsh
. /home/tng/.asdf/asdf.sh
source <(kubectl completion zsh)
