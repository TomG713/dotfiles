# shortcut to this dotfiles path is $ZSH

##### timer for troubleshooting
# timer=$(($(gdate +%s%N)/1000000))
# now=$(($(gdate +%s%N)/1000000))
# elapsed=$(($now-$timer))
# echo $elapsed":" $plugin
### or unncomment and run zprof
# zmodload zsh/zprof


###### ohmyzsh
# https://github.com/ohmyzsh/ohmyzsh
export ZSH="$HOME/.oh-my-zsh"
ZSH_THEME=""
zstyle ':omz:update' mode auto      # update automatically without asking
source $ZSH/oh-my-zsh.sh
######


##### prompt
# oh-my-zsh overrides the prompt, so Pure must be activated after `source $ZSH/oh-my-zsh.sh`
# seems to be required to work on linux, zplug generates errors
fpath+=($HOME/.zsh/pure)
autoload -U promptinit; promptinit
prompt pure
#####

##### zplug 
# https://github.com/zplug/zplug
source ~/.zplug/init.zsh
zplug 'zplug/zplug', hook-build:'zplug --self-manage'

zplug "zsh-users/zsh-syntax-highlighting"
zplug "zsh-users/zsh-autosuggestions"

# https://github.com/sindresorhus/pure
# zplug "sindresorhus/pure", use:pure.zsh, from:github, as:theme


# https://github.com/mroth/evalcache
zplug "mroth/evalcache"

# https://github.com/lukechilds/zsh-nvm
export NVM_LAZY_LOAD=true
zplug "lukechilds/zsh-nvm"

# https://github.com/ohmyzsh/ohmyzsh/tree/master/plugins/kubectl
zplug "plugins/kubectl",   from:oh-my-zsh

# https://github.com/ohmyzsh/ohmyzsh/blob/master/plugins/git/README.md
zplug "plugins/git",  from:oh-my-zsh

if ! zplug check --verbose; then
    printf "Install? [y/N]: "
    if read -q; then
        echo; zplug install
    fi
fi
zplug load
# zplug update > /dev/null 
#####

##### 
if which rbenv > /dev/null; then _evalcache rbenv init -; fi
if which pyenv > /dev/null; then _evalcache "$(pyenv init -)"; fi
if which pyenv-virtualenv-init > /dev/null; then _evalcache "$(pyenv virtualenv-init -)"; fi
if which jenv > /dev/null; then _evalcache jenv init -; fi

if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi


export DOTFILES=$HOME/.dotfiles
typeset -U config_files
config_files=($DOTFILES/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files

